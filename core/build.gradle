plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def propertiesFile = rootProject.file("core/src/main/assets/version.properties")
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

android {
    namespace 'com.truvideo.sdk.core'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionName properties.getProperty("versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions = ['environments']

    productFlavors {
        dev {
            dimension 'environments'
        }
        beta {
            dimension 'environments'
        }
        rc {
            dimension 'environments'
        }
        prod {
            dimension 'environments'
        }
    }
}

dependencies {
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation 'androidx.activity:activity-compose:1.9.0'

    // S3
    implementation 'com.amazonaws:aws-android-sdk-mobile-client:2.16.6'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.44.0'
    implementation 'com.amazonaws:aws-android-sdk-cognito:2.16.6'

    //Common
    def commonVersion = properties.getProperty("commonVersion")
    implementation("com.github.Truvideo:truvideo-sdk-android-common:$commonVersion")

    // Common dependencies
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
    implementation("io.ktor:ktor-client-core:2.3.10")
    implementation("io.ktor:ktor-client-logging:2.3.2")
    implementation("io.ktor:ktor-client-okhttp:2.3.10")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0")
    implementation("com.russhwolf:multiplatform-settings:1.0.0")
    implementation("org.jetbrains.kotlinx:kotlinx-datetime:0.4.1")
    implementation("co.touchlab:kermit:2.0.0-RC5")
    implementation("org.kodein.di:kodein-di:7.21.1")

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:1.6.1"
    testImplementation "org.mockito:mockito-core:5.4.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
    testImplementation "io.mockk:mockk:1.13.5"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId 'com.github.Truvideo'
                setArtifactId 'truvideo-sdk-android-core-local'
                version android.defaultConfig.versionName

                def environment = properties.getProperty("environment")
                switch (environment) {
                    case 'dev':
                        artifact bundleDevReleaseAar
                        break

                    case 'beta':
                        artifact bundleBetaReleaseAar
                        break

                    case 'rc':
                        artifact bundleRcReleaseAar
                        break

                    case 'prod':
                        artifact bundleProdReleaseAar
                        break
                }

                pom.withXml {
                    //noinspection GroovyImplicitNullArgumentCall
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}